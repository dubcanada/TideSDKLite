#!/usr/bin/env python

# This file has been modified from its orginal sources.
#
# Copyright (c) 2012 Software in the Public Interest Inc (SPI)
# Copyright (c) 2012 David Pratt
# 
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Copyright (c) 2008-2012 Appcelerator Inc.
# 
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import os
import os.path as path
import effess
Import('build')
Import('LIBTIDE_NAME')

libtide_env = build.env.Clone()

libtide_name = LIBTIDE_NAME

sources = Glob('*.cpp') + \
    Glob('config/*.cpp') + \
    Glob('binding/*.cpp') + \
    Glob('net/proxy_config.cpp') + \
    Glob('javascript/*.cpp') + \
    Glob('api/*.cpp')

if build.is_linux():
    sources += Glob('linux/*.cpp')

if build.is_win32():
    sources += Glob('win32/*.cpp')

env = libtide_env
env.Append(CPPDEFINES=[('TIDE_API_EXPORT', 1)])
build.add_thirdparty(env, 'poco')
build.add_thirdparty(env, 'webkit')
build.add_thirdparty(env, 'boost')
if build.is_osx():
    build.add_thirdparty(env, 'openssl')
    sources +=  Glob('osx/*.mm') + \
        Glob('net/*_%s.mm' % build.os)

env.Append(CPPPATH=[path.join(build.tide_source_dir, 'lib', 'tide')])
if build.is_linux():
    if os.environ.has_key('PKG_CONFIG_PATH'):
        env.AppendENVPath('PKG_CONFIG_PATH', os.environ['PKG_CONFIG_PATH'])
    env.ParseConfig('pkg-config --cflags --libs libxml-2.0 gtk+-2.0 gdk-2.0 glib-2.0 gthread-2.0')
    env.Append(LIBS=['pthread', 'libsoup-2.4', 'libproxy', 'libgcrypt', 'libgnutls'])
    build.add_thirdparty(env, 'libproxy')

if build.is_win32():
    env.Append(CPPPATH=[path.join(build.third_party, 'icu', 'include')])
    env.Append(LIBS=[
        'Advapi32', 'comctl32',
        'iphlpapi', 'kernel32',
        'ole32', 'oleaut32', 'pthreadVC2',
        'shell32', 'shlwapi', 'user32',
        'winhttp',
    ])
    if build.tidelite is False:
        env.Append(LIBS=['icuuc','icuin'])
    else:
        env.Append(LIBS=['libicuuc','libicuin', 'icudt46'])

    env.Append(CCFLAGS=['/DUNICODE', '/D_UNICODE'])
    env.Append(LINKFLAGS=['/LTCG', '/INCREMENTAL:NO'])
    build.add_thirdparty(env, 'libproxy')

elif build.is_osx():
    env.Append(LINKFLAGS='-install_name lib' + libtide_name + '.dylib')
    env.Append(LIBS=['ssl', 'crypto'])
    env.Append(FRAMEWORKS=['Cocoa', 'SystemConfiguration', 'CoreServices'])

build.mark_build_target(
    env.SharedLibrary(path.join(build.dir, 'runtime', libtide_name), source=sources))
build.utils.LightWeightCopy(path.join(build.cwd(), 'support'), path.join(build.dir, 'runtime'))

# copy the includes for building the SDK
tide_include = path.join(build.tide_include_dir, LIBTIDE_NAME)
effess.copy_tree(build.cwd(), tide_include, include=['.h'])
